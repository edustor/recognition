import org.ajoberstar.grgit.Grgit

import java.time.Instant

buildscript {
    ext.kotlin_version = '1.1.2-2'
    ext.spring_boot_version = '1.5.3.RELEASE'

    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.springframework.boot:spring-boot-gradle-plugin:$spring_boot_version"
        classpath 'org.ajoberstar:gradle-git:1.6.0'
    }
}

plugins {
    id "com.palantir.configuration-resolver" version "0.1.0"
}

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'spring-boot'

group 'ru.edustor'
version '0.9-SNAPSHOT'

repositories {
    mavenCentral()
}

configurations {
    all*.exclude group: "log4j"
}

//noinspection GroovyAssignabilityCheck
jar {
    destinationDir = new File(buildDir, "/dist")
    archiveName = "edustor-recognition.jar"
}

ext {
    def grgit = Grgit.open()
    shortCommitId = grgit.head().getAbbreviatedId()
    commitId = grgit.head().id
}

task createProperties(dependsOn: processResources) {
    doLast {
        new File("$buildDir/resources/main/build.properties").withWriter { w ->
            def p = new Properties()
            p['edustor.build.name'] = "Edustor Recognition"
            p['edustor.build.version'] = "${project.version}-$shortCommitId".toString()
            p['edustor.build.date'] = Instant.now().epochSecond.toString()
            p['edustor.build.commitId'] = commitId
            p['edustor.build.repo'] = "https://github.com/edustor/recognition"
            p.store(w, null)
        }
    }
}
processResources.finalizedBy createProperties

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile 'org.ghost4j:ghost4j:1.0.1'
    compile 'com.google.zxing:javase:3.3.0'
    compile 'io.minio:minio:2.0.4'
    compile 'com.itextpdf:kernel:7.0.1'
    compile 'commons-codec:commons-codec:1.10'

    compile "org.springframework.boot:spring-boot-autoconfigure"
    compile "org.springframework.boot:spring-boot-starter-web"
    compile "org.springframework.boot:spring-boot-starter-logging"
    compile "org.springframework.boot:spring-boot-starter-amqp"

    compile project(':edustor-commons:version')
    compile project(':edustor-commons:storage')
    compile project(':edustor-commons:rabbit')
    compile project(':edustor-commons:models')

    compile 'net.java.dev.jna:jna:4.1.0' // ghost4j doesn't support jna 4.2.2

    testCompile "org.springframework.boot:spring-boot-starter-test"
}